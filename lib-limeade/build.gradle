plugins {
	alias(libs.plugins.java.library)
	alias(libs.plugins.java.test.fixtures)
	alias(libs.plugins.post.compile.weaving)
	alias(libs.plugins.maven.publish)
}

// fix name of published test fixtures capability: lib-limeade -> limeade
afterEvaluate {
	configurations.named("testFixturesApiElements") {
		outgoing {
			capability("${rootProject.group}:${project.artifactId}-test-fixtures:${project.version}")
		}
	}
	configurations.named("testFixturesRuntimeElements") {
		outgoing {
			capability("${rootProject.group}:${project.artifactId}-test-fixtures:${project.version}")
		}
	}
	configurations.named("testFixturesSourcesElements") {
		outgoing {
			capability("${rootProject.group}:${project.artifactId}-test-fixtures:${project.version}")
		}
	}
}

dependencies {
	aspect libs.bundles.lambda.powertools
	implementation platform(libs.aws.sdk.bom)
	implementation libs.aspectjrt

	implementation platform(libs.opentelemetry.bom.alpha)
	implementation libs.cri.common.lib
	implementation libs.opentelemetry.aws.sdk
	implementation libs.bundles.aws.sdk.dynamodb
	implementation libs.bundles.lambda.powertools
	implementation libs.bundles.apache.http

	testImplementation libs.bundles.webcompere

	testFixturesImplementation platform(libs.junit.bom)
	testFixturesImplementation platform(libs.mockito.bom)
	testFixturesImplementation libs.bundles.junit
	testFixturesImplementation libs.bundles.mockito
	testFixturesImplementation libs.bundles.apache.http
	testFixturesImplementation libs.bundles.lambda.powertools
}


tasks.withType(Javadoc).configureEach {
	options.addStringOption('Xdoclint:all,-missing', '-quiet')
	// FIXME: silence javadoc warnings for deprecated packages, remove me
	exclude '**/ipv/cri/lime/limeade/service/http/retryer/**'
	exclude '**/ipv/cri/lime/limeade/testfixtures/HttpRetryStatusConfigFixtures.java'
}
